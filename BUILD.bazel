# SPDX-License-Identifier: GPL-2.0

load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("//common:modules.bzl", "COMMON_GKI_MODULES_LIST")
load("//build/bazel_common_rules/exec:exec.bzl", "exec")
load("//build/bazel_common_rules/dist:dist.bzl", "copy_to_dist_dir")
load(
    "//build/kernel/kleaf:kernel.bzl",
    "kernel_abi",
    "kernel_build",
    "kernel_images",
    "kernel_module",
    "kernel_module_group",
    "kernel_modules_install",
    "kernel_unstripped_modules_archive",
    "merged_kernel_uapi_headers",
)
load("@kernel_toolchain_info//:dict.bzl", "CLANG_VERSION")

_SOC_REVS = [
    "a0",
    "b0",
    "b0_v2-ipop",
]

_LYNX_DTBOS = [
    # Sync with build.config.lynx
    "gs201-lynx-dev1_0.dtbo",
    "gs201-lynx-pvt1_0.dtbo",
    "gs201-lynx-dvt1_0-gain1.dtbo",
    "gs201-lynx-mp1_0.dtbo",
    "gs201-lynx-proto1_1.dtbo",
    "gs201-lynx-evt1_1.dtbo",
    "gs201-lynx-evt1_0.dtbo",
    "gs201-lynx-evt1_0-wingboard.dtbo",
    "gs201-lynx-proto1_0.dtbo",
    "gs201-lynx-dvt1_0.dtbo",
]

kernel_module_group(
    name = "lynx_ext_modules",
    srcs = [
        # do not sort
        "//private/devices/google/lynx:gs201_soc",
        "//private/devices/google/lynx/display:drm_panel.google",
        "//private/google-modules/display:samsung",
        "//private/google-modules/gpu/mali_kbase",
        "//private/google-modules/gpu/mali_pixel",
        "//private/google-modules/edgetpu/janeiro/drivers/edgetpu:edgetpu.janeiro",
        "//private/google-modules/gxp/gs201:gxp",
        "//private/google-modules/bms",
        "//private/google-modules/power/reset",
        "//private/google-modules/lwis",
        "//private/google-modules/amplifiers/audiometrics",
        "//private/google-modules/amplifiers/snd_soc_wm_adsp:snd-soc-wm-adsp",
        "//private/google-modules/amplifiers/cs35l41",
        "//private/google-modules/amplifiers/cs35l45",
        "//private/google-modules/amplifiers/cs40l26",
        "//private/google-modules/amplifiers/drv2624",
        "//private/google-modules/aoc",
        "//private/google-modules/aoc/alsa",
        "//private/google-modules/nfc",
        "//private/google-modules/touch/common:touch.common",
        "//private/google-modules/touch/focaltech/ft3658",
        "//private/google-modules/touch/goodix",
        "//private/google-modules/video/gchips:video.gchips",
    ],
)

kernel_build(
    name = "lynx",
    srcs = glob([
        "build.config.*",
        "dts/**",
    ]) + [
        # keep sorted
        "//common:kernel_aarch64_sources",
        "//private/google-modules/power/reset:reset.kconfig",
        "//private/google-modules/soc/gs:arch/arm64/configs/cloudripper_gki.fragment",
        "//private/google-modules/soc/gs:build.config.gs101",
        "//private/google-modules/soc/gs:gs_soc.kconfig",
        "//private/google-modules/touch/common:common.kconfig",
        "//private/google-modules/touch/fts/ftm5:ftm5.kconfig",
        "//private/google-modules/touch/sec:sec.kconfig",
        "//private/google-modules/video/gchips:video.gchips.kconfig",
    ],
    outs = [
        "google-base/gs201-{}.dtb".format(soc_rev)
        for soc_rev in _SOC_REVS
    ] + _LYNX_DTBOS,
    base_kernel = "//common:kernel_aarch64_download_or_build",
    build_config = "build.config.lynx",
    collect_unstripped_modules = True,
    dtstree = "//private/devices/google/lynx/dts:lynx.dt",
    kconfig_ext = "//private/google-modules/soc/gs:Kconfig.ext",
    kmi_symbol_list = "//common:android/abi_gki_aarch64_pixel",
    module_outs = [
        # keep sorted
        "crypto/crct10dif_common.ko",
        "crypto/crct10dif_generic.ko",
        "drivers/block/null_blk/null_blk.ko",
        "drivers/hwtracing/coresight/coresight.ko",
        "drivers/hwtracing/coresight/coresight-etm4x.ko",
        "drivers/hwtracing/coresight/coresight-funnel.ko",
        "drivers/hwtracing/coresight/coresight-replicator.ko",
        "drivers/hwtracing/coresight/coresight-tmc.ko",
        "drivers/i2c/i2c-dev.ko",
        "drivers/misc/eeprom/at24.ko",
        "drivers/perf/arm_dsu_pmu.ko",
        "drivers/pps/clients/pps-gpio.ko",
        "drivers/scsi/scsi_debug.ko",
        "drivers/scsi/sg.ko",
        "drivers/spi/spidev.ko",
        "drivers/watchdog/softdog.ko",
        "lib/crc-t10dif.ko",
        "net/mac80211/mac80211.ko",
        "net/wireless/cfg80211.ko",
    ],
    strip_modules = True,
    # Keep in sync with build.config.common
    toolchain_version = CLANG_VERSION,
    visibility = [
        # keep sorted
        "//private/google-modules:__subpackages__",
    ],
)

kernel_abi(
    name = "lynx_abi",
    kernel_build = ":lynx",
    kernel_modules = [":lynx_ext_modules"],
    kmi_symbol_list_add_only = True,
    module_grouping = False,
)

kernel_module(
    name = "gs201_soc",
    srcs = [
        "//private/google-modules/soc/gs:gs_soc_sources",
    ],
    outs = [
        # keep sorted
        "drivers/bts/bts.ko",
        "drivers/clk/gs/clk_exynos_gs.ko",
        "drivers/clocksource/exynos_mct.ko",
        "drivers/cpufreq/exynos-acme.ko",
        "drivers/devfreq/google/arm-memlat-mon.ko",
        "drivers/devfreq/google/exynos_devfreq.ko",
        "drivers/devfreq/google/governor_memlat.ko",
        "drivers/devfreq/google/memlat-devfreq.ko",
        "drivers/dma-buf/heaps/samsung/samsung_dma_heap.ko",
        "drivers/dma/pl330.ko",
        "drivers/dma/samsung-dma.ko",
        "drivers/gpu/exynos/g2d/g2d.ko",
        "drivers/i2c/busses/i2c-acpm.ko",
        "drivers/i2c/busses/i2c-exynos5.ko",
        "drivers/iio/power/odpm.ko",
        "drivers/input/fingerprint/goodixfp.ko",
        "drivers/input/keyboard/s2mpg12-key.ko",
        "drivers/input/keycombo.ko",
        "drivers/input/keydebug.ko",
        "drivers/input/misc/vl53l1/stmvl53l1.ko",
        "drivers/iommu/exynos-pcie-iommu.ko",
        "drivers/iommu/samsung-iommu-group.ko",
        "drivers/iommu/samsung-secure-iova.ko",
        "drivers/iommu/samsung_iommu.ko",
        "drivers/media/platform/exynos/mfc/exynos_mfc.ko",
        "drivers/media/platform/exynos/smfc/smfc.ko",
        "drivers/mfd/s2mpg12-mfd.ko",
        "drivers/mfd/s2mpg13-mfd.ko",
        "drivers/mfd/s2mpg1x-gpio.ko",
        "drivers/mfd/slg51000-core.ko",
        "drivers/mfd/slg51002-core.ko",
        "drivers/misc/bbdpl/bcm47765.ko",
        "drivers/misc/gvotable.ko",
        "drivers/misc/logbuffer.ko",
        "drivers/misc/sbb-mux/sbb-mux.ko",
        "drivers/misc/sscoredump/sscoredump.ko",
        "drivers/pci/controller/dwc/pcie-exynos-gs.ko",
        "drivers/pci/controller/dwc/pcie-exynos-gs201-rc-cal.ko",
        "drivers/phy/samsung/phy-exynos-mipi.ko",
        "drivers/phy/samsung/phy-exynos-mipi-dsim.ko",
        "drivers/phy/samsung/phy-exynos-usbdrd-super.ko",
        "drivers/pinctrl/gs/pinctrl-exynos-gs.ko",
        "drivers/pinctrl/pinctrl-slg51000.ko",
        "drivers/pinctrl/pinctrl-slg51002.ko",
        "drivers/power/reset/debug-reboot.ko",
        "drivers/regulator/pmic_class.ko",
        "drivers/regulator/s2mpg12-powermeter.ko",
        "drivers/regulator/s2mpg12-regulator.ko",
        "drivers/regulator/s2mpg13-powermeter.ko",
        "drivers/regulator/s2mpg13-regulator.ko",
        "drivers/regulator/slg51000-regulator.ko",
        "drivers/regulator/slg51002-regulator.ko",
        "drivers/rtc/rtc-s2mpg12.ko",
        "drivers/scsi/ufs/ufs-exynos-gs.ko",
        "drivers/soc/google/acpm/acpm_flexpmu_dbg.ko",
        "drivers/soc/google/acpm/acpm_mbox_test.ko",
        "drivers/soc/google/acpm/gs_acpm.ko",
        "drivers/soc/google/acpm/power_stats.ko",
        "drivers/soc/google/bcm_dbg.ko",
        "drivers/soc/google/cal-if/cmupmucal.ko",
        "drivers/soc/google/cpif/boot_device_spi.ko",
        "drivers/soc/google/cpif/cp_thermal_zone.ko",
        "drivers/soc/google/cpif/cpif.ko",
        "drivers/soc/google/cpif/cpif_page.ko",
        "drivers/soc/google/cpif/shm_ipc.ko",
        "drivers/soc/google/dbgcore-dump.ko",
        "drivers/soc/google/debug/debug-snapshot-debug-kinfo.ko",
        "drivers/soc/google/debug/dss.ko",
        "drivers/soc/google/debug/ehld.ko",
        "drivers/soc/google/debug/etm2dram.ko",
        "drivers/soc/google/debug/exynos-adv-tracer.ko",
        "drivers/soc/google/debug/exynos-adv-tracer-s2d.ko",
        "drivers/soc/google/debug/exynos-coresight.ko",
        "drivers/soc/google/debug/exynos-coresight-etm.ko",
        "drivers/soc/google/debug/exynos-debug-test.ko",
        "drivers/soc/google/debug/exynos-ecc-handler.ko",
        "drivers/soc/google/debug/itmon.ko",
        "drivers/soc/google/debug/pixel-boot-metrics.ko",
        "drivers/soc/google/debug/pixel-debug-test.ko",
        "drivers/soc/google/debug/pixel-suspend-diag.ko",
        "drivers/soc/google/debug/sjtag-driver.ko",
        "drivers/soc/google/ect_parser.ko",
        "drivers/soc/google/eh/eh.ko",
        "drivers/soc/google/exynos-bcm_dbg-dump.ko",
        "drivers/soc/google/exynos-cpuhp.ko",
        "drivers/soc/google/exynos-cpupm.ko",
        "drivers/soc/google/exynos-dm.ko",
        "drivers/soc/google/exynos-pd.ko",
        "drivers/soc/google/exynos-pd-dbg.ko",
        "drivers/soc/google/exynos-pd_el3.ko",
        "drivers/soc/google/exynos-pm.ko",
        "drivers/soc/google/exynos-pmu-if.ko",
        "drivers/soc/google/exynos-seclog.ko",
        "drivers/soc/google/exynos_pm_qos.ko",
        "drivers/soc/google/gs-chipid.ko",
        "drivers/soc/google/gsa/gsa.ko",
        "drivers/soc/google/gsa/gsa_gsc.ko",
        "drivers/soc/google/hardlockup-debug.ko",
        "drivers/soc/google/hardlockup-watchdog.ko",
        "drivers/soc/google/kernel-top.ko",
        "drivers/soc/google/pixel_stat/mm/pixel_stat_mm.ko",
        "drivers/soc/google/pixel_stat/pixel_stat_sysfs.ko",
        "drivers/soc/google/pkvm-s2mpu/pkvm-s2mpu.ko",
        "drivers/soc/google/pt/slc_acpm.ko",
        "drivers/soc/google/pt/slc_dummy.ko",
        "drivers/soc/google/pt/slc_pmon.ko",
        "drivers/soc/google/pt/slc_pt.ko",
        "drivers/soc/google/sysrq-hook.ko",
        "drivers/soc/google/vh/kernel/systrace.ko",
        "drivers/spi/spi-s3c64xx.ko",
        "drivers/thermal/google/google_bcl.ko",
        "drivers/thermal/google/s2mpg13_spmic_thermal.ko",
        "drivers/thermal/samsung/gpu_cooling.ko",
        "drivers/thermal/samsung/gs_thermal.ko",
        "drivers/trusty/trusty-core.ko",
        "drivers/trusty/trusty-ipc.ko",
        "drivers/trusty/trusty-irq.ko",
        "drivers/trusty/trusty-log.ko",
        "drivers/trusty/trusty-test.ko",
        "drivers/trusty/trusty-virtio.ko",
        "drivers/tty/serial/exynos_tty.ko",
        "drivers/usb/dwc3/dwc3-exynos-usb.ko",
        "drivers/usb/gadget/function/usb_f_dm.ko",
        "drivers/usb/gadget/function/usb_f_dm1.ko",
        "drivers/usb/host/xhci-exynos.ko",
        "drivers/usb/typec/tcpm/google/bc_max77759.ko",
        "drivers/usb/typec/tcpm/google/max77759_contaminant.ko",
        "drivers/usb/typec/tcpm/google/max77759_helper.ko",
        "drivers/usb/typec/tcpm/google/tcpci_max77759.ko",
        "drivers/usb/typec/tcpm/google/usb_psy.ko",
        "drivers/usb/typec/tcpm/google/usbc_cooling_dev.ko",
        "drivers/watchdog/s3c2410_wdt.ko",
    ],
    kernel_build = "//private/google-modules/soc/gs:gs_kernel_build",
    makefile = ["//private/google-modules/soc/gs:Makefile"],
    visibility = [
        # keep sorted
        "//private/devices/google:__subpackages__",
        "//private/google-modules:__subpackages__",
    ],
)

kernel_modules_install(
    name = "lynx_modules_install",
    kernel_build = ":lynx",
    kernel_modules = [":lynx_ext_modules"],
)

merged_kernel_uapi_headers(
    name = "lynx_merged_uapi_headers",
    kernel_build = ":lynx",
    kernel_modules = [":lynx_ext_modules"],
)

kernel_unstripped_modules_archive(
    name = "lynx_unstripped_modules_archive",
    kernel_build = ":lynx",
    kernel_modules = [":lynx_ext_modules"],
)

genrule(
    name = "vendor_kernel_boot_modules.lynx-cat",
    srcs = [
        "vendor_kernel_boot_modules.lynx",
        "//build/kernel:hermetic-tools/cat",
        "//private/google-modules/soc/gs:vendor_kernel_boot_modules.gs201",
    ],
    outs = [
        "vendor_kernel_boot_modules.concat",
    ],
    # TODO(b/259274243): switch to using the hermetic-tools cat binary
    cmd = """
        cat $(location vendor_kernel_boot_modules.lynx) \\
            $(location //private/google-modules/soc/gs:vendor_kernel_boot_modules.gs201) \\
            > $@
    """,
    tools = ["//build/kernel:hermetic-tools/cat"],
)

write_file(
    name = "system_dlkm_modules_list",
    out = "system_dlkm_modules_list.lynx",
    content = COMMON_GKI_MODULES_LIST,
)

kernel_images(
    name = "lynx_images",
    base_kernel_images = "//common:kernel_aarch64_images_download_or_build",
    boot_image_outs = ["dtb.img"],
    build_boot = False,
    build_dtbo = True,
    build_initramfs = True,
    build_system_dlkm = True,
    build_vendor_dlkm = True,
    build_vendor_kernel_boot = True,
    dedup_dlkm_modules = True,
    dtbo_srcs = [":lynx/" + file for file in _LYNX_DTBOS],
    kernel_build = ":lynx",
    kernel_modules_install = ":lynx_modules_install",
    # Keep the following in sync with build.config.lynx:
    modules_list = ":vendor_kernel_boot_modules.lynx-cat",
    system_dlkm_modules_list = ":system_dlkm_modules_list",
    system_dlkm_props = "//private/google-modules/soc/gs:system_dlkm.props.gs201",
    vendor_dlkm_etc_files = ["//private/devices/google/lynx:insmod_cfgs"],
    vendor_dlkm_modules_blocklist = "//private/google-modules/soc/gs:vendor_dlkm.blocklist.cloudripper",
    # No MODULES_BLOCKLIST
    vendor_dlkm_modules_list = "//private/google-modules/soc/gs:vendor_dlkm_modules.slider",
    vendor_dlkm_props = "//private/google-modules/soc/gs:vendor_dlkm.props.slider",
    deps = [
        # Keep the following in sync with vendor_dlkm.props.slider:
        # selinux_fc
        "//prebuilts/boot-artifacts/selinux:file_contexts",
        "//prebuilts/boot-artifacts/selinux:system_file_contexts",
    ],
)

filegroup(
    name = "insmod_cfgs",
    srcs = glob([
        "insmod_cfg/*.cfg",
    ]),
)

_lynx_dist_targets = [
    ":lynx",
    ":lynx_images",
    "//private/google-modules/soc/gs:gki_aarch64_boot",
    # At the time of writing (2022-02-04), this intentionally diverges from
    # the behavior of build.sh-style mixed builds by also incorporating
    # UAPI headers of external modules, while build.sh-style mixed builds
    # always uses kernel-uapi-headers.tar.gz from GKI_DIST_DIR.
    # To use GKI's kernel-uapi-headers.tar.gz in DIST_DIR, use
    #     //common:kernel_aarch64_uapi_headers
    # instead.
    ":lynx_merged_uapi_headers",
    ":lynx_modules_install",
    ":lynx_unstripped_modules_archive",
    ":insmod_cfgs",
    "//common:android/abi_gki_aarch64_pixel",
    "//common:kernel_aarch64_download_or_build",
] + ["//common:kernel_aarch64/{}".format(m) for m in COMMON_GKI_MODULES_LIST]

copy_to_dist_dir(
    name = "lynx_dist_copy_to_dist_dir",
    data = _lynx_dist_targets,
    dist_dir = "out/lynx/dist",
    flat = True,
    log = "info",
)

exec(
    name = "lynx_dist",
    data = [":lynx_dist_copy_to_dist_dir"],
    script = """
        echo {gki_kernel_dir}: $(git -C $BUILD_WORKSPACE_DIRECTORY/{gki_kernel_dir} rev-parse HEAD)
        $(rootpath :lynx_dist_copy_to_dist_dir) $@
    """.format(gki_kernel_dir = "//common"),
)
